plugins {
    id 'java'
    id 'maven-publish'
    id "com.gradleup.shadow" version "8.3.0"
}

defaultTasks 'build'

group 'eu.decentsoftware.holograms'
version '2.8.9'
description 'A lightweight yet very powerful hologram plugin with many features and configuration options.'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://repo.codemc.io/repository/nms/' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.13-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.2'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'de.tr7zw:item-nbt-api:2.12.4'

    // Annotations
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    compileOnly 'org.jetbrains:annotations:24.1.0'
    annotationProcessor 'org.jetbrains:annotations:24.1.0'

    implementation 'com.github.retrooper:packetevents-spigot:2.4.0'
    implementation 'com.github.Tofaa2.EntityLib:spigot:2.4.9-SNAPSHOT'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.17.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifactId 'decentholograms'
        }
    }
}

processResources {
    def props = [version: version, description: project.description]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate "org.bstats", "eu.decentsoftware.holograms.metrics"
    relocate "de.tr7zw.changeme.nbtapi", "eu.decentsoftware.holograms.libs.nbtapi"

    relocate "com.github.retrooper", "eu.decentsoftware.holograms.libs.packetevents.api"
    relocate "io.github.retrooper.packetevents", "eu.decentsoftware.holograms.libs.packetevents.impl"
    relocate "net.kyori", "eu.decentsoftware.holograms.libs.kyori"
}